%mask-immediate { #80 }
%mask-length    { #3f }


@forth-init
  ;tib .in STZ2
  ;memory .here-pos STZ2
  #10 .base STZ
  ;Builtin-emit .dictptr STZ2
JMP2r


@on-console-forth
  .in LDZ2 ;tib/end EQU2 ,&tib-overflow JCN

  .Console/read DEI
  DUP newline EQU ,&newline JCN
  .in LDZ2 STA
  .in ;inc-zptr JSR2
JMP2r


&newline ( newline -- )
  POP
  null .in LDZ2 STA

  ;interpret JSR2
  ;&done ;println JSR2

  ;tib .in STZ2
  ;prompt ;print-str JSR2
BRK
&done
03 20 "ok 00

&tib-overflow
    ;Error/tib-overflow ;print-str JSR2
    HALT
BRK


@inc-zptr ( zaddr -- )
  STHk LDZ2
  INC
  STHr STZ2
JMP2r


@read-word ( -- *str )
  .in LDZ2 #0001 SUB2 ;word STA2

&loop
  .in LDZ2 LDA
  ( DUP ;print-byte JSR2 space EMIT DUP EMIT CR )

  DUP space EQU ,&space JCN
  DUP null  EQU ,&null  JCN

  ( everything else )
  POP
  .in ;inc-zptr JSR2
  ,&loop JMP

&space
  POP
  null .in LDZ2 STA
  ;word LDA2
  ,&calc-len JSR
  .in ;inc-zptr JSR2
JMP2r

&null
  POP
&end
  ;word LDA2
  ,&calc-len JSR
JMP2r

&calc-len
  .in LDZ2 ;word LDA2 INC2 SUB2
  NIP
  ;word LDA2 STA
JMP2r


@isnt-immediate ( addr -- b8 )
  LDA mask-immediate AND
  mask-immediate NEQ
JMP2r


@dbg
  .compiling LDZ ,&c JCN
  LIT 'i EMIT
  ,&next JMP

&c
  LIT 'c EMIT
&next
  space EMIT
  ;print-str JSR2
  CR
JMP2r


@interpret ( -- )
  ;tib .in STZ2
&loop
  ;read-word JSR2
  DUP2 INC2 LDA #00 EQU ;&end JCN2 ( end of line )

  DUP2 ;dbg JSR2

  DUP2 ;lookup JSR2
  DUP2 #0000 NEQ2 ,&word JCN

  ;interpret-number JSR2

  ;&loop JMP2

&word
  ;interpret-word JSR2
  ;&loop JMP2

&end
JMP2r


@interpret-word ( *str *dict -- )
  NIP2 ( drop str )
  DUP2 ;isnt-immediate JSR2
  .compiling LDZ
  AND
  ,&compile-word JCN
&execute-word
  ;execute JSR2
JMP2r

&compile-word
  ;push JSR2
  ;comma16 JSR2
JMP2r

@interpret-number ( *str #0000 -- )
  POP2
  ;parse-number JSR2
  ,&error JCN

  .compiling LDZ ,&compile-number JCN

  ( DUP ;print-short JSR2 CR )
  ;push JSR2
JMP2r
&error
  ;Error/unknown-word ;println JSR2
JMP2r

&compile-number
  ;lit16
  ;push JSR2
  ;comma16 JSR2
  ;push JSR2
  ;comma16 JSR2
JMP2r


@lookup ( *str -- &dictnode )
  STH2
  .dictptr LDZ2

&loop ( *dictnode )
  DUP2 #0000 EQU2 ,&end JCN

  ( LIT 'l EMIT LIT 'u EMIT space EMIT DUP2 ;println JSR2 )
  DUP2
  ( DUP2 #03 ;dump JSR2 )
  INC2
  DUP2 #0001 SUB2 LDA mask-length AND
  STH2rk
  ( OVR2 #03 ;dump JSR2 )
  INC2
  ;memcmp JSR2 ,&end JCN

&next
  ;dict-link JSR2
  LDA2

  ,&loop JMP

&end
  POP2r
JMP2r


@dict-link ( *dict -- *link )
  LDAk
  mask-length AND
  #00 SWP ADD2 ( skip strlen )
  #0002 ADD2 ( skip length byte and null byte )
JMP2r

@dict-code ( *dict -- *link )
  LDAk
  mask-length AND
  #00 SWP ADD2 ( skip strlen )
  #0004 ADD2 ( skip length byte, null byte and link field )
JMP2r


@tos ( -- )
  ;Wst
  #00 ;Wst/ptr LDA
  ADD2
JMP2r


@push ( u16 -- )
  ( DUP2 ;print-short JSR2 CR )
  ;tos JSR2

  DUP2 ;Wst/end GEQ2 ,&stack-overflow JCN
  
  STA2

  ;Wst/ptr LDA #02 ADD ;Wst/ptr STA
JMP2r

&stack-overflow
  ;Error/stack-overflow ;println JSR2
  HALT
JMP2r


@pop ( -- u16 )
  ( calculate tos )
  ;tos JSR2

  DUP2 ;Wst EQU2 ,&stack-underflow JCN

  #0002 SUB2
  LDA2

  ;Wst/ptr LDA #02 SUB ;Wst/ptr STA
JMP2r

&stack-underflow
  ;Error/stack-underflow ;println JSR2
  HALT
JMP2r


@execute ( &dictnode -- )
  ;dict-code JSR2
  LDA2 JSR2
JMP2r


@dup ( -- )
  ;pop JSR2
  DUP2
  ;push JSR2
  ;push JSR2
JMP2r

@dot ( -- )
  ;pop JSR2
  ;print-short JSR2
JMP2r


@add ( -- )
  ;pop JSR2
  ;pop JSR2
  ADD2
  ;push JSR2
JMP2r


@minus ( -- )
  ;pop JSR2
  ;pop JSR2
  SWP2
  SUB2
  ;push JSR2
JMP2r

@mult ( -- )
  ;pop JSR2
  ;pop JSR2
  MUL2
  ;push JSR2
JMP2r


@div ( -- )
  ;pop JSR2
  ;pop JSR2
  SWP2
  DIV2
  ;push JSR2
JMP2r


@emit ( -- )
  ;pop JSR2
  ( DUP2 ;print-short JSR2 CR )
  ( DUP2 #0020 LTH2 ,&error JCN )
  DUP2 #007e GTH2 ,&error JCN

  EMIT
  POP

JMP2r

&error
  ;print-short JSR2 space EMIT
  ;&not-ascii ;println JSR2
  HALT
JMP2r
&not-ascii 09 "not-ascii 00


@lit16 ( -- )
  ;&name ;println JSR2
  ;Error/todo ;println JSR2
  HALT
JMP2r
&name 05 "lit16 00


@docol ( -- )
  ;&name ;println JSR2
  ;Error/todo ;println JSR2
  HALT
JMP2r
&name 05 "docol 00


@exit ( -- )
  ;&name ;println JSR2
  ;Error/todo ;println JSR2
  HALT
JMP2r
&name 04 "exit 00


@print-stack ( -- )
  ( calculate tos )
  ;Wst
  #00 ;Wst/ptr LDA
  ADD2
  STH2
  ;Wst

&loop
  DUP2 STH2rk EQU2 ,&end JCN
  LDA2k ;print-short JSR2
  space EMIT
  #0002 ADD2
  ,&loop JMP

&end
  POP2
  POP2r
JMP2r


@colon ( -- )
  ;read-word JSR2
  ( LIT '# EMIT space EMIT DUP2 ;println JSR2 )
  LDAk #00 EQU ,&error JCN
  ( LIT '# EMIT space EMIT DUP2 ;println JSR2 )
  ;create-word JSR2
  ;docol
  ;push JSR2
  ;comma16 JSR2
  true .compiling STZ
JMP2r
&error
  ;Error/missing-word ;println JSR2
JMP2r


@create-word ( *str -- )
  ( CR LIT 'c EMIT LIT 'w EMIT space EMIT DUP2 ;println JSR2 )
  .here-pos LDZ2 STH2 ( store new dictptr )

  LDAk #00 SWP
  ;push JSR2
  ;comma8 JSR2

  LDAk INC STH

&loop
  INC2
  ( LDAk ;debug-ascii JSR2 )
  LDAk #00 SWP
  ;push JSR2
  ;comma8 JSR2
  STHr #01 SUB STHk
  #00 EQU ,&end JCN
  ,&loop JMP

&end
  POP2
  POPr

  ( $2 link )
  .dictptr LDZ2
  ;push JSR2
  ;comma16 JSR2

  STH2r .dictptr STZ2
JMP2r


@semi-colon
  false .compiling STZ
  ;exit
  ;push JSR2
  ;comma16 JSR2
JMP2r


@push-here
  .here-pos LDZ2
  ;push JSR2
JMP2r

@inc-here8
  .here-pos LDZ2
  INC2
  .here-pos STZ2
JMP2r

@inc-here16
  .here-pos LDZ2
  INC2
  INC2
  .here-pos STZ2
JMP2r

@peek16
  ;pop JSR2
  DUP2 ;print-short JSR2 CR
  LDA2
  DUP2 ;print-short JSR2 CR
  ;push JSR2
JMP2r

@poke16
  ;pop JSR2
  ;pop JSR2
  SWP2
  STA2
JMP2r

@peek8
  #00
  ;pop JSR2
  LDA
  ;push JSR2
JMP2r

@poke8
  ;pop JSR2
  ;pop JSR2
  NIP ROT ROT
  STA
JMP2r

@comma16
  ;push-here JSR2
  ;poke16 JSR2
  ;inc-here16 JSR2
JMP2r

@comma8
  ;push-here JSR2
  ;poke8 JSR2
  ;inc-here8 JSR2
JMP2r

~string.tal

@prompt
00 "> 20 00


@Error
&invalid-char    00 "invalid 20 "char: 20 00
&todo            00 "todo 00
&tib-overflow    00 "tib 20 "overflow 00
&stack-overflow  00 "stack 20 "overflow 00
&stack-underflow 00 "stack 20 "underflow 00
&unknown-word    00 "unknown-word 00
&missing-word    00 "missing-word 00


$1
@tib
$ff
&end

@Wst
$fd
&end
&err $1
&ptr $1

@Rst
$fd
&end
&err $1
&ptr $1

~dict.tal

@memory

|0000
@in $2
@word $2
@here-pos $2
@dictptr $2
@base $1
@compiling $1



