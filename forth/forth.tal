%mask-immediate { #80 }
%mask-hidden    { #40 }
%mask-length    { #3f }


@forth-init
  #d0 .wst STZ
  #d0 .rst STZ
  ;swap-stacks JSR2
  ;tib .in STZ2
  ;memory .here-pos STZ2
  #10 .base STZ
  ;Builtin-emit .latest STZ2

JMP2r


@e?
  LIT 'e EMIT
  .error LDZ ;print-byte JSR2
  CR
JMP2r


@on-console-forth
  ( check for overflow )
  .in LDZ2 ;tib/end EQU2 ,&tib-overflow JCN

  ( read char )
  .Console/read DEI

  ( DUP EMIT )

  ( check for newline )
  DUP newline EQU ,&newline JCN

  ( store char, and increment pointer )
  .in LDZ2 STA
  .in ;inc-zptr JSR2
BRK


&newline ( newline -- )
  POP
  ( terminal tib with null char )
  null .in LDZ2 STA

  ;interpret JSR2
  .error LDZ ,&error JCN
  ;&done ;println JSR2

  ;tib .in STZ2
  ;prompt ;print-str JSR2
BRK
&done
03 20 "ok 00

&error
BRK

&tib-overflow
    ;Error/tib-overflow ;print-str JSR2
    HALT
BRK


@inc-zptr ( zaddr -- )
  DUP LDZ2
  INC
  ROT STZ2
JMP2r


@read-word ( -- *str )
  .in LDZ2 #0001 SUB2 ;word STA2

&loop
  ( load next char )
  .in LDZ2 LDA
  ( DUP ;print-byte JSR2 space EMIT DUP EMIT CR )

  ( check char )
  DUP space EQU ,&space JCN
  DUP null  EQU ,&null  JCN

  ( everything else )
  POP
  .in ;inc-zptr JSR2
  ,&loop JMP

&space
  POP
  null .in LDZ2 STA
  ;word LDA2
  ,&calc-len JSR
  .in ;inc-zptr JSR2
JMP2r

&null
  POP
&end
  ;word LDA2
  ,&calc-len JSR
JMP2r

&calc-len
  .in LDZ2 ;word LDA2 INC2 SUB2
  NIP
  ;word LDA2 STA
JMP2r


@isnt-immediate ( addr -- b8 )
  INC2 INC2
  LDA mask-immediate AND
  mask-immediate NEQ
JMP2r


@dbg
  .state LDZ ,&c JCN
  LIT 'i EMIT
  ,&next JMP
&c
  LIT 'c EMIT
&next
  space EMIT
  ;print-cstr JSR2
  CR
JMP2r


@interpret ( -- )
  ;tib .in STZ2
  ( CR .in LDZ2 ;print-cstr JSR2 CR )
&loop
  .error LDZ #01 EQU ,&end JCN
  ;read-word JSR2

  ( LIT 'w EMIT space EMIT )
  ( DUP2 ;print-str JSR2 CR )

  ( DUP2 ;dbg JSR2 )

  ( read first char )
  DUP2 INC2 LDA

  ( check for end of line )
  null EQU ;&end JCN2

  DUP2 ;lookup JSR2
  DUP2 #0000 NEQ2 ,&word JCN

  ;interpret-number JSR2

  ;&loop JMP2

&word
  ;interpret-word JSR2
  ;&loop JMP2

&end
JMP2r


@interpret-word ( *str *dict -- )
  ( OVR2 ;print-str JSR2 CR
  DUP2 #10 ;dump JSR2 )

  NIP2 ( drop str )

  DUP2 ;isnt-immediate JSR2
  .state LDZ
  AND
  ,&compile-word JCN

&execute-word
  .temp STZ2
  ;swap-stacks JSR2
  .temp LDZ2

  ;dict-code JSR2
  ( DUP2 #10 ;dump JSR2 CR )
  ( DUP2 #10 ;dis JSR2 CR )
  ( execute )
  JSR2

  ;swap-stacks JSR2
JMP2r

&compile-word
  ;dict-code JSR2
  ;compile-call JSR2
JMP2r


@swap-stacks
  .System/wst DEI
  STH
  .wst LDZ
  .System/wst DEO
  STHr
  .wst STZ

  ( keep return address )
  STH2r

  .System/rst DEI
  .rst LDZ
  .System/rst DEO
  .rst STZ

  STH2
JMP2r


@interpret-number ( *str #0000 -- )
  POP2
  ;parse-number JSR2
  ,&error JCN

  .state LDZ ,&compile-number JCN


  ( DUP ;print-short JSR2 CR )
  .temp STZ2
  ;swap-stacks JSR2
  .temp LDZ2
  ;swap-stacks JSR2
JMP2r
&error
  ;Error/unknown-word ;println JSR2
  #01 .error STZ
JMP2r

&compile-number
  #00 LIT LIT2
  ;comma8 JSR2

  ;comma16 JSR2
JMP2r


@lookup ( *str -- &dictnode )
  ( DUP2 ;print-str JSR2 CR )
  STH2

  .latest LDZ2
  ( DUP2 #10 ;dump JSR2 CR )

&loop ( *dictnode )
  DUP2 #0000 EQU2 ,&end JCN
  ( DUP2 #10 ;dump JSR2 CR )

  DUP2
  ( skip link and length )
  #0003 ADD2
  ( DUP2 ;print-cstr JSR2 CR )
  ( DUP2 ;print-cstr JSR2 space EMIT )

  ( load length )
  STH2rk LDA
  ( DUP ;print-byte JSR2 space EMIT )

  ( load *cstr )
  STH2rk INC2
  ( DUP2 ;print-cstr JSR2 CR )

  ;memcmp JSR2

  ,&match JCN

  ( next )
  LDA2
  ( DUP2 #10 ;dump JSR2 CR )

  ,&loop JMP

&match
  ( ;&s-matched ;println JSR2 )
&end
  POP2r
JMP2r
&s-lookup 07 "lookup 20 00
&s-matched 07 "matched 00


@dict-code ( *dict -- *link )
  ( debug )
  ( DUP2 #10 ;dump  JSR2 )

  ( skip link )
  INC2 INC2

  ( read length )
  LDAk mask-length AND

  ( skip strlen )
  #00 SWP ADD2

  ( skip flag and null char )
  INC2 INC2

  ( DUP2 ;print-short JSR2 CR )
JMP2r


@dup ( -- )
  DUP2
JMP2r


@print-tos ( -- )
  ;print-short JSR2
JMP2r


@add ( -- )
  ADD2
JMP2r


@minus ( -- )
  SUB2
JMP2r

@mult ( -- )
  MUL2
JMP2r


@div ( -- )
  DIV2
JMP2r


@emit ( -- )
  ( DUP2 ;print-short JSR2 CR )
  NIP
  ( DUP #20 LTH ,&error JCN )
  DUP #7e GTH ,&error JCN

  EMIT
JMP2r

&error
  ;print-short JSR2 space EMIT
  ;&not-ascii ;println JSR2
  HALT
JMP2r
&not-ascii 09 "not-ascii 00


@print-stack ( -- )
JMP2r


@colon ( -- )
  ;read-word JSR2
  ( LIT '# EMIT space EMIT DUP2 ;print-str JSR2 CR )

  ( check fo null string )
  LDAk #00 EQU ,&error JCN

  ;create-word JSR2
  ;mark-hidden JSR2

  true .state STZ
JMP2r
&error
  ;Error/missing-word ;println JSR2
JMP2r


@create-word ( *str -- )
  ( CR LIT 'c EMIT LIT 'w EMIT space EMIT DUP2 ;println JSR2 )

  ( read new latest )
  .here-pos LDZ2

  ( write link )
  .latest LDZ2
  ;comma16 JSR2

  ( write new latest )
  .latest STZ2

  ( write string length )
  LDAk #00 SWP
  ;comma8 JSR2

  ( stash string length )
  LDAk INC STH

  ( write string )
&loop

  INC2
  ( LDAk ;debug-ascii JSR2 )

  LDAk #00 SWP
  ;comma8 JSR2

  #01 STH SUBr

  ( check for null )
  LDAk #00 EQU ,&end JCN
  ,&loop JMP

&end
  POP2 ( char )
  POPr ( counter )

JMP2r

@mark-hidden
  ( get flag pointer )
  .latest LDZ2 INC2 INC2

  ( mark it )
  LDAk mask-hidden ORA

  ( save it )
  ROT ROT
  STA
JMP2r


@clear-hidden
  ( get flag pointer )
  .latest LDZ2 INC2 INC2

  ( clear it )
  LDAk
  mask-hidden EOR

  ( save it )
  ROT ROT STA
JMP2r


@compile-call ( addr16 -- )
  ( LIT2 addr16 JSR2 )
  LIT LIT2
  ROT ROT
  LIT JSR2
  SWP2

  ;comma16 JSR2
  ;comma16 JSR2
JMP2r


@semi-colon
  ;clear-hidden JSR2

  #00 LIT JMP2r
  ;comma8 JSR2

  false .state STZ
JMP2r


@push-here
  .here-pos LDZ2
JMP2r

@inc-here8
  .here-pos LDZ2
  INC2
  .here-pos STZ2
JMP2r

@inc-here16
  .here-pos LDZ2
  INC2
  INC2
  .here-pos STZ2
JMP2r

@peek16
  DUP2 ;print-short JSR2 CR
  LDA2
  DUP2 ;print-short JSR2 CR
JMP2r

@poke16
  STA2
JMP2r

@peek8 ( u16 -- u16 )
  LDA
  ( convert to u16 )
  #00 SWP
JMP2r

@poke8 ( u16 addr16 -- )
  STA
  POP
JMP2r

@comma16
  ;push-here JSR2
  ;poke16 JSR2
  ;inc-here16 JSR2
JMP2r

@comma8
  ;push-here JSR2
  ;poke8 JSR2
  ;inc-here8 JSR2
JMP2r


~string.tal

@prompt
00 "> 20 00


@Error
&invalid-char    00 "invalid 20 "char: 20 00
&todo            00 "todo 00
&tib-overflow    00 "tib 20 "overflow 00
&stack-overflow  00 "stack 20 "overflow 00
&stack-underflow 00 "stack 20 "underflow 00
&unknown-word    00 "unknown-word 00
&missing-word    00 "missing-word 00


$1
@tib
$ff
&end

~dict.tal

@memory

|0000
@in $2
@word $2
@here-pos $2
@latest $2
@base $1
@state $1
@nextp $2
@error $1
@wst $1
@rst $1
@temp $2



